	
on pick up:
	if event-item's name is "Puck":
		cancel event
		if {ballkick} is not set:
			set {ballkick} to now
		if difference between {ballkick} and now is more than 3 ticks:
			set {ballkick} to now
			if distance between event-item entity and player is less than 1.25:
				add event-item entity to team named "Puck"
				if event-player is sneaking:
					play sound "minecraft:block.stone.break" at volume 1 at pitch 0.5 on event-player
					cancel event
					push event-item entity (direction from player to event-item entity) at speed 0.1
				else if player is sprinting:
					play sound "minecraft:block.stone.break" at volume 1 at pitch 0.7 on event-player
					cancel event
					if distance between event-player and block below player is more than 0.6:
						push event-item entity (direction from player to event-item entity) at speed 0.6
					else:
						push event-item entity (direction from player to event-item entity) at speed 0.75
				else:
					play sound "minecraft:block.stone.break" at volume 1 at pitch 0.5 on event-player
					cancel event
					push event-item entity (direction from player to event-item entity) at speed 0.6

######################


on left click:
	if distance between event-player's targeted entity and player is less than 4:
		if item of player's targeted entity is polished blackstone slab named "Puck":
			push event-player's targeted entity (direction from player to event-player's targeted entity) at speed 0.6
			play sound "minecraft:block.wood.place" with volume 10 with pitch 1 at event-location
			add event-player's targeted entity to team named "Puck"


##################


on item despawn:
	if event-item's name is "Puck":
		cancel event
		

		
on entity inside block:
	if event-block is honey block:
		push event-entity (direction from event-block to event-entity) at speed 0.35

on entity inside block:
	if event-block is tripwire:
		if name of item of event-entity is "Puck":
			kill event-entity
			show huge explosion at event-location
			if block 2 blocks below event-block is red wool:
				add 1 to {team1score}
				set display text of {team1display} to "%{team1score}%"
			if block 2 blocks below event-block is blue wool:
				add 1 to {team2score}
				set display text of {team2display} to "%{team2score}%"
			play sound "minecraft:entity.firework_rocket.large_blast" at volume 1 at pitch 0.8 on event-block
			drop polished blackstone slab named "Puck" at {puckspawn} without velocity

############### Game Settings ################


command /setpuckspawn:
	permission: op
	trigger:
		set {puckspawn} to player's location

on load:
	set {timertextdisplay} to nearest text display relative to {timerdisloc}
	set {team1display} to nearest text display relative to {team1dloc}
	set {team2display} to nearest text display relative to {team2dloc}


################ Score ##################

command /scorereset:
	permission: op
	trigger:
		set display scale of {team1display} to vector 5, 5, 5
		set display scale of {team2display} to vector 5, 5, 5
		set {team1score} to 0
		set {team2score} to 0
		set yaw of {team1display} to 180
		set yaw of {team2display} to 180
		set display text of {team1display} to "%{team1score}%"
		set display text of {team2display} to "%{team2score}%"
		

command /setteam1display:
	permission: op
	trigger:
		set {team1display} to nearest text display relative to player
		set {team1dloc} to player's location
command /setteam2display:
	permission: op
	trigger:
		set {team2display} to nearest text display relative to player
		set {team2dloc} to player's location
		

	
	
	
	
	
################ Timer ##################

command /settimerdisplay:
	permission: op
	trigger:
		set {timertextdisplay} to nearest text display relative to player
		set {timerdisloc} to player's location

every 1 second:
	if {timeron} is true:
		if {timerseconds} is 0:
			set {timerseconds} to 60
			remove 1 from {timerminutes}
		remove 1 from {timerseconds}
		if {timerseconds} is less than 10:
			set {timertext} to "%{timerminutes}%:0%{timerseconds}%"
			
		else:
			set {timertext} to "%{timerminutes}%:%{timerseconds}%"
		set display text of {timertextdisplay} to {timertext}
		if {timerminutes} is 0:
			if {timerseconds} is 0:
				execute console command "/timerreset"
		
command /timerreset:
	permission: op
	trigger:
		set yaw of {timertextdisplay} to 180
		set {timeron} to false
		set {timerseconds} to 00
		set {timerminutes} to 30
		set {timertext} to "%{timerminutes}%:%{timerseconds}%0"
		set display scale of {timertextdisplay} to vector 4, 4, 4
		set display text of {timertextdisplay} to {timertext}
		play sound "minecraft:item.goat_horn.sound.0" to all players
command /timerstart:
	permission: op
	trigger:
		set {timeron} to true
		play sound "minecraft:item.goat_horn.sound.0" to all players
command /timerpause:
	permission: op
	trigger:
		set {timeron} to false
		
##### ELO CALC #####
